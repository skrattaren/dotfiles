prompt_squiggle_pp_help() {
    cat <<'EOF'
A colourful prompt, with SCM info, jobs, return code and histoff marker.
Pretty Prompt, named after Lupin Squiggle Sec'y pp
Originally it was based on Gentoo prompt.  Colour-scheme-able:

    prompt squiggle_pp [<pathcolour> [<rootcolour> [<vcsinfocolour> \
                       [<jobnumcolour> [<trailing%colour>]]]]]

EOF
}

prompt_squiggle_pp_setup() {
    clr_path=${1:-'green'}
    clr_root=${2:-'red'}
    clr_vcs=${3:-'cyan'}
    clr_job=${4:-'magenta'}
    clr_trail=${5:-'blue'}

    jobs="%F{$clr_job}%(1j. [%j].)%f"

    if [[ "$USER" = 'root' ]]
    then
        base_prompt="%B%F{$clr_root}%m%k "
    else
        base_prompt="%B "
    fi

    hist_mark="%S%v%s%f"

    path_prompt="%F{$clr_path}%(5~|%-1~/…/%3~|%4~) "
    vcs_prompt='%F{$clr_vcs}${vcs_info_msg_0_:+${vcs_info_msg_0_} }%f'
    post_prompt="%b%f%k "

    PS1="${hist_mark}${jobs}${base_prompt}${path_prompt}${vcs_prompt}"
    PS1="${PS1}%B%F{$clr_trail}%(0?.%#.%S%#%s)$post_prompt"
    PS2="$base_prompt$path_prompt %_> $post_prompt"
    PS3="$base_prompt$path_prompt ?# $post_prompt"

    # vcs_info options
    autoload -Uz vcs_info
    zstyle ':vcs_info:*' enable git hg
    zstyle ':vcs_info:*' check-for-changes true
    zstyle ':vcs_info:*' unstagedstr '%U'
    zstyle ':vcs_info:hg:*' get-revision true
    zstyle ':vcs_info:hg:*' get-mq true
    zstyle ':vcs_info:hg:*' formats '[☿:%u%b%m%%u]'
    zstyle ':vcs_info:hg:*' actionformats '[☿:%u%b%%u-%a]'
    zstyle ':vcs_info:hg:*' branchformat '%b'
    zstyle ':vcs_info:hg:*' patch-format '+%p'
    zstyle ':vcs_info:hg:*' nopatch-format ''
    zstyle ':vcs_info:git:*' formats '[±:%u%b%%u]'
    zstyle ':vcs_info:git:*' actionformats '[±:%u%b%%u|%a]'

    add-zsh-hook precmd vcs_info
    add-zsh-hook precmd() {
        if [[ -z $HISTFILE ]]; then
            psvar[1]=" H "
        else
            psvar[1]=""
        fi
        print -Pn "\e]0;%~ %#\a"
        echo -ne '\a'
    }
}

prompt_squiggle_pp_setup "$@"

# vim:filetype=zsh
